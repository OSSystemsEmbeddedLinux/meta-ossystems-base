# Copyright (c) 2015-2020 O.S. Systems Software LTDA.
#
# The ossystems-factory-defaults class provides some variables that
# can be configured.
#
# OSSYSTEMS_FACTORY_DEFAULTS_FILES: files to be added to the factory
# default configuration.
# Default: empty
#
# OSSYSTEMS_FACTORY_DEFAULTS_POSTINST_FILES: files generated by
# postinst tasks to be added to the factory default configuration.
# Default: empty
#
# OSSYSTEMS_FACTORY_DEFAULTS_DIR: directory where factory default
# configuration is stored.
# Default: "${datadir}/factory-defaults"
#
# OSSYSTEMS_FACTORY_DEFAULTS_RUNTIME_DIR: directory where
# configuration files link to.  Usually in a filesystem mounted with
# read-write permissions.
# Default: /data
#
# OSSYSTEMS_FACTORY_DEFAULTS_HOOKS: files to be installed in
# $OSSYSTEMS_FACTORY_DEFAULTS_HOOKS_DIR.  The syntax for referencing
# files is the same as for BitBake's SRC_URI.
# Default: empty.
#
# OSSYSTEMS_FACTORY_DEFAULTS_HOOKS_DIR: directory for hook programs.  Hook
# programs are run by factory-defaults right after restoring the
# default configuration.
# Default: ${sysconfdir}/factory-defaults.d

inherit ossystems-factory-defaults-base

member() {
    elt=$1
    shift
    items=$*
    for item in $items; do
        [ "$item" = "$elt" ] && return 0
    done
    return 1
}

SRC_URI += "${OSSYSTEMS_FACTORY_DEFAULTS_HOOKS}"

do_install:append() {
    install -d ${D}${OSSYSTEMS_FACTORY_DEFAULTS_DIR}

    # Check if files exist
    for file in ${OSSYSTEMS_FACTORY_DEFAULTS_FILES}; do
        if [ ! -e "${D}$file" ] && ! member $file ${OSSYSTEMS_FACTORY_DEFAULTS_POSTINST_FILES}; then
            bberror "Could not find $file, referenced in \$OSSYSTEMS_FACTORY_DEFAULTS_FILES."
        fi
    done

    # Copy files to the factory directory
    cd ${D}
    no_leading_slash=`echo ${OSSYSTEMS_FACTORY_DEFAULTS_FILES} | sed 's+^/++; s+ /+ +g'`
    no_leading_slash="$no_leading_slash `echo ${OSSYSTEMS_FACTORY_DEFAULTS_POSTINST_FILES} | sed 's+^/++; s+ /+ +g'`"
    [ "${OSSYSTEMS_FACTORY_DEFAULTS_FILES}" ] && \
        tar cf - $no_leading_slash | tar xf - -C ${D}${OSSYSTEMS_FACTORY_DEFAULTS_DIR}

    # Create symlinks
    local dir
    for file in $no_leading_slash; do
        dir="${D}/`dirname $file`"
        mkdir -p $dir
        cd $dir
        ln -sf ${OSSYSTEMS_FACTORY_DEFAULTS_RUNTIME_DIR}/$file `basename $file`
    done

    # Install hooks
    if [ -n "${OSSYSTEMS_FACTORY_DEFAULTS_HOOKS}" ]; then
        install -d ${D}${OSSYSTEMS_FACTORY_DEFAULTS_HOOKS_DIR}
        for hook in ${OSSYSTEMS_FACTORY_DEFAULTS_HOOKS}; do
            hook="`echo $hook | sed -r 's,^[^:]+://,,'`" # drop <scheme>://
            install -m 755 ${WORKDIR}/$hook ${D}${OSSYSTEMS_FACTORY_DEFAULTS_HOOKS_DIR}
        done
    fi
}

PACKAGES:prepend = "${PN}-ossystems-factory-defaults "

FILES:${PN}-ossystems-factory-defaults += "\
    ${OSSYSTEMS_FACTORY_DEFAULTS_RUNTIME_DIR} \
    ${OSSYSTEMS_FACTORY_DEFAULTS_DIR} \
    ${OSSYSTEMS_FACTORY_DEFAULTS_HOOKS_DIR} \
"

RDEPENDS:${PN} += "ossystems-factory-defaults ${PN}-ossystems-factory-defaults"
