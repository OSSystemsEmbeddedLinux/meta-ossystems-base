DISTRO_VERSION = "14.2+snapshot-${DATE}"

SDK_VENDOR = "-oelsdk"
SDK_VERSION := "${@'${DISTRO_VERSION}'.replace('snapshot-${DATE}','snapshot')}"

MAINTAINER = "O.S. Systems Software LTDA. <contato@ossystems.com.br>"

TARGET_VENDOR = "-oel"

LOCALCONF_VERSION = "1"

# Target i686 for the sdk binaries, rather than the build arch.
#
# Other options: i586, x86_64
SDKMACHINE ?= "i686"

# Paths
OELDIR ?= "${COREBASE}/.."
DEPLOY_DIR_IMAGE = "${DEPLOY_DIR}/images/${MACHINE}"
SDKPATH = "/opt/${DISTRO}/${SDK_VERSION}"

# Move the persist db up out of TMPDIR so it survives its removal
PERSISTENT_DIR = "${TOPDIR}/db"

# Restore any available saved headrevs
DUMP_HEADREVS_DB ?= '${OELDIR}/${MACHINE}/saved_persist_data.db'
INHERIT += "restore-dumped-headrevs"

require conf/distro/include/oel-providers.conf

# Classes we inherit by default
INHERIT_DISTRO ?= "debian devshell sstate license deploy-license-manifest"

# Do an up front type check to sanity check user configuration
INHERIT += "typecheck"

# Lower the priority of meta-oe, as we prefer oe-core when recipes are duped
BBFILE_PRIORITY_openembedded-layer = "1"

# locales shouldn't be in lib32/lib64
localedir = "${exec_prefix}/lib/locale"

# Ensure that we implement shared state reuse handling for non-target recipes
require conf/distro/include/sstate.inc

# A problem we have is that while the info page for ld says that sysv is the
# default in RHEL5, it is actually not and it defaults to gnu hash.  This
# in turn results in binaries that won't run on older systems.  We change to
# forcing both hash types to be included to fix this issue.
BUILD_LDFLAGS += "-Wl,--hash-style=both"

# Quadruple the normal. 'du' is not a good way to really see how much
# space will be needed and fails badly as the fs size grows.
IMAGE_ROOTFS_EXTRA_SPACE = "40960"

# Override these in oel based distros
OEL_DEFAULT_DISTRO_FEATURES = "largefile opengl multiarch"
OEL_DEFAULT_EXTRA_RDEPENDS = "packagegroup-core-boot"
OEL_DEFAULT_EXTRA_RRECOMMENDS = ""

DISTRO_FEATURES ?= "${DISTRO_FEATURES_DEFAULT} \
                    ${DISTRO_FEATURES_LIBC} \
                    ${OEL_DEFAULT_DISTRO_FEATURES}"

PREFERRED_VERSION_linux-yocto ?= "3.19%"
PREFERRED_VERSION_linux-yocto_qemux86 ?= "3.19%"
PREFERRED_VERSION_linux-yocto_qemux86-64 ?= "3.19%"
PREFERRED_VERSION_linux-yocto_qemuarm ?= "3.19%"
PREFERRED_VERSION_linux-yocto_qemumips ?= "3.19%"
PREFERRED_VERSION_linux-yocto_qemumips64 ?= "3.19%"
PREFERRED_VERSION_linux-yocto_qemuppc ?= "3.19%"

DISTRO_EXTRA_RDEPENDS += " ${OEL_DEFAULT_EXTRA_RDEPENDS}"
DISTRO_EXTRA_RRECOMMENDS += " ${OEL_DEFAULT_EXTRA_RRECOMMENDS}"

# Remove .la files for packages, as they really aren't of much use. This is
# also needed for ADE construction to align with the Yocto SDK, though that
# could be handled via path exclusions with opkg-p2 as well.
DISTRO_FEATURES_BACKFILL_CONSIDERED += "libtool-garbage"
INHERIT += "libtool-garbage"

# Add nls if we're supporting.
USE_NLS ??= "yes"

# Align the nls distro feature with USE_NLS
DISTRO_FEATURES_append = "${@' nls' if '${USE_NLS}' == 'yes' else ''}"

# Ensure fbset is in busybox configuration, and fbset-modes is included
PACKAGECONFIG_append_pn-busybox = " fbset"

# Ensure that we are not getting GUI for latencytop, as we don't want x11 to
# be pulled into our console image.
PACKAGECONFIG_pn-latencytop = ""

# Ensure that we are not getting GUI for sysprof, as we don't want x11 to
# be pulled into our console image.
PACKAGECONFIG_pn-sysprof = ""

# Enable systemd in pulseaudio when appropriate
PACKAGECONFIG_append_pn-pulseaudio = "${@bb.utils.contains('DISTRO_FEATURES', 'systemd', ' systemd', '', d)}"

# Override pulseaudio to use a system rather than user systemd service when we
# don't have pam available, as the user service won't be started
systemd_userunitdir_pn-pulseaudio = "${@bb.utils.contains('DISTRO_FEATURES', 'pam', '${systemd_unitdir}/user', '${systemd_unitdir}/system', d)}"

OELQEMUDEPS = "${@bb.utils.contains("INCOMPATIBLE_LICENSE", "GPLv3", "", "packagegroup-core-device-devel",d)}"
DISTRO_EXTRA_RDEPENDS_append_qemuarm = " ${OELQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemuarm64 = " ${OELQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemumips = " ${OELQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemuppc = " ${OELQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemux86 = " ${OELQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemux86-64 = " ${OELQEMUDEPS}"

TCLIBCAPPEND = ""

QEMU_TARGETS ?= "arm aarch64 i386 mips mipsel mips64 ppc x86_64"
# Other QEMU_TARGETS "mips64el sh4"

# We prefer busybox rather than tinylogin
VIRTUAL-RUNTIME_login_manager = "busybox"

# Sane default locales for images
GLIBC_GENERATE_LOCALES ?= "en_US en_US.UTF-8"
IMAGE_LINGUAS ?= "en-us"

# Targets for qemu
QEMU_TARGETS += "mips64 mips64el sh4"

# Use a local PR server by default
PRSERV_HOST ?= "localhost:0"

PREMIRRORS_append = " \
git://sources.redhat.com/ git://sourceware.org/ \n\
"

MIRRORS += "\
${KERNELORG_MIRROR}/ https://kernel.googlesource.com/ \n\
${KERNELORG_MIRROR}/ http://mirror.nexcess.net/kernel.org/ \n\
${KERNELORG_MIRROR}/ http://mirror.gbxs.net/pub/ \n\
${APACHE_MIRROR}/ http://archive.apache.org/dist/ \n\
${DEBIAN_MIRROR}/ ftp://archive.debian.org/debian/pool/ \n\
git://git.kernel.org/pub/ http://mirror.nexcess.net/kernel.org/ \n\
\
(ftp|https?)://.*/.* http://autobuilder.yoctoproject.org/sources/ \n\
(ftp|https?)://.*/.* http://sources.openembedded.org/ \n\
(ftp|https?)://.*/.* http://www.angstrom-distribution.org/unstable/sources/ \n\
\
(cvs|svn|git|gitsm|hg|bzr|osc|p4)://.*/.* http://downloads.yoctoproject.org/mirror/sources/ \n\
(cvs|svn|git|gitsm|hg|bzr|osc|p4)://.*/.* http://autobuilder.yoctoproject.org/sources/ \n\
(cvs|svn|git|gitsm|hg|bzr|osc|p4)://.*/.* http://sources.openembedded.org/ \n\
(cvs|svn|git|gitsm|hg|bzr|osc|p4)://.*/.* http://www.angstrom-distribution.org/unstable/sources/ \n\
"


SANITY_TESTED_DISTROS ?= " \
            Ubuntu-12.04 \n \
            Ubuntu-14.04 \n \
            Ubuntu-14.10 \n \
            Fedora-20 \n \
            CentOS-6.* \n \
            CentOS-7.* \n \
            Debian-7.* \n \
            Debian-8.* \n \
            openSUSE-project-12.3 \n \
            openSUSE-project-13.1 \n \
            "

# Default hash policy for distro
BB_SIGNATURE_HANDLER ?= 'OEBasicHash'
#
# OELAYOUT_ABI allows us to notify users when the format of TMPDIR changes in 
# an incompatible way. Such changes should usually be detailed in the commit
# that breaks the format and have been previously discussed on the mailing list 
# with general agreement from the core team.
#
OELAYOUT_ABI = "10"

# QA check settings - a little stricter than the OE-Core defaults
WARN_TO_ERROR_QA = "already-stripped compile-host-path install-host-path \
                    installed-vs-shipped ldflags pn-overrides rpaths staticdev \
                    useless-rpaths"
WARN_QA_remove = "${WARN_TO_ERROR_QA}"
ERROR_QA_append = " ${WARN_TO_ERROR_QA}"

# We want information about image contents
INHERIT += "buildhistory"
BUILDHISTORY_DIR ?= "${TOPDIR}/buildhistory"
BUILDHISTORY_COMMIT ?= "1"

# Allow blacklisting recipes
INHERIT += "blacklist"

# More sane usability for the archiver classes
ARCHIVER_MODE ?= "none"
ARCHIVER_MODE[type] = "choice"
ARCHIVER_MODE[choices] = "none original patched configured"
ARCHIVER_CLASS = "${@'archive-${ARCHIVER_MODE}-source' \
                     if ARCHIVER_MODE != 'none' else ''}"
INHERIT += "${ARCHIVER_CLASS}"

# Ensure we have license-filtered sources available
COPYLEFT_LICENSE_INCLUDE = '*'
COPYLEFT_LICENSE_EXCLUDE = 'CLOSED Proprietary* Freescale EULA INTEL NetLogic'
COPYLEFT_RECIPE_TYPES ?= '${COPYLEFT_AVAILABLE_RECIPE_TYPES}'
INHERIT += "copyleft_compliance"

# Ensure we have what we need for the below type checks
OE_IMPORTS += "oe.terminal"

# The user's shell shouldn't be allowed to affect the build (and it can break
# the flock command, if the user's shell isn't POSIX compliant). This should
# go upstream.
SHELL[unexport] = "1"
BB_TERMINAL_EXPORTS += "SHELL"

# Fix upstream image.bbclass vardeps bug, to ensure that changes to the image
# features result in re-executing do_rootfs.
FEATURE_INSTALL[vardepvalue] = "${FEATURE_INSTALL}"
FEATURE_INSTALL_OPTIONAL[vardepvalue] = "${FEATURE_INSTALL_OPTIONAL}"

# Changing IMAGE_FSTYPES should re-run do_rootfs. This belongs upstream.
do_rootfs[vardeps] += "IMAGE_FSTYPES"

# Changing a mirror shouldn't force a refetch/rebuild
SRC_URI[vardepsexclude] += "\
    APACHE_MIRROR \
    CPAN_MIRROR \
    DEBIAN_MIRROR \
    E_MIRROR \
    GENTOO_MIRROR \
    GNOME_GIT \
    GNOME_MIRROR \
    GNU_MIRROR \
    GPE_MIRROR \
    KERNELORG_MIRROR \
    SOURCEFORGE_MIRROR \
    XLIBS_MIRROR \
    XORG_MIRROR \
"

# Disable reliance upon upstream URIs, as we want our customers to be able to
# build without network connectivity
CONNECTIVITY_CHECK_URIS = ""
